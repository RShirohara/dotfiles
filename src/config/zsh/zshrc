# Run commands at the start of an interactive sessions.

## Initialize
### Enable powerlevel10k instant prompt.
if [[ -r "${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### Set Zi path.
typeset -A ZI
ZI[HOME_DIR]="${XDG_DATA_HOME}/zsh/zi"
ZI[BIN_DIR]="${ZI[HOME_DIR]}/bin"
ZI[COMPINIT_OPTS]="-C"

### Load Zi.
source "${ZI[BIN_DIR]}/zi.zsh"

### Enable Zi completions.
autoload -Uz _zi
(( ${+_comps} )) && _comps[zi]=_zi

## Plugins
### Zsh settings
zi is-snippet lucid for \
  "${ZDOTDIR}/modules/settings/completion.zsh" \
  "${ZDOTDIR}/modules/settings/directory.zsh" \
  "${ZDOTDIR}/modules/settings/expansion.zsh" \
  "${ZDOTDIR}/modules/settings/history.zsh" \
  "${ZDOTDIR}/modules/settings/io.zsh" \
  "${ZDOTDIR}/modules/settings/script.zsh" \
  "${ZDOTDIR}/modules/settings/external-command.zsh"

### Prezto modules
zi is-snippet lucid for \
  "${ZDOTDIR}/modules/prezto/init.zsh" \
  "${ZDOTDIR}/modules/prezto/editor.zsh" \
  "${ZDOTDIR}/modules/prezto/terminal.zsh" \
  PZTM::environment \
  PZTM::editor \
  PZTM::terminal

### Zi annexes
zi light-mode lucid for \
  @z-shell/z-a-bin-gem-node \
  @z-shell/z-a-patch-dl

### Plugins
#### Theme
zi light-mode lucid for \
  depth=1 @romkatv/powerlevel10k \
  is-snippet "${ZDOTDIR}/modules/powerlevel10k/p10k.zsh"

zi light-mode lucid pack for \
  @z-shell/dircolors-material

#### Syntax highlight
zi-turbo "0a" for \
  atinit="zicompinit; zicdreplay" @z-shell/F-Sy-H

#### Completion
zi-turbo "0a" for \
  blockf atpull="zi creinstall -q ." @zsh-users/zsh-completions \
  @z-shell/zsh-fancy-completions \
  @Aloxaf/fzf-tab \
  is-snippet "${ZDOTDIR}/modules/fzf/setting.zsh"

#### Suggestion
zi-turbo "0a" for \
  atload="!_zsh_autosuggest_start" @zsh-users/zsh-autosuggestions

#### Auto-pair delimiters
zi light-mode lucid for \
  @hlissner/zsh-autopair

#### VSCode integration
zi is-snippet lucid has="code" if='[[ "${TERM_PROGRAM}" == "vscode" ]]' for \
  "$(code --locate-shell-integration-path zsh)" \
  "${ZDOTDIR}/modules/vscode/shell-integration.zsh"

### External commands
zi light-mode lucid as="program" from="gh-r" blockf for \
  sbin="**/rg" \
    cp="**/doc/rg.1 -> ${ZPFX}/man/man1/rg.1" \
    @BurntSushi/ripgrep \
  sbin="**/delta" \
    dl="https://raw.githubusercontent.com/dandavison/delta/main/etc/completion/completion.zsh -> _delta" \
    @dandavison/delta \
  sbin="**/eza" \
    dl="https://raw.githubusercontent.com/eza-community/eza/main/completions/zsh/_eza -> _eza" \
    @eza-community/eza \
  sbin="**/bin/teip" \
    cp="**/man/teip.1 -> ${ZPFX}/man/man1/teip.1" \
    @greymd/teip \
  sbin="**/fzf" \
    dl="https://raw.githubusercontent.com/junegunn/fzf/master/shell/completion.zsh -> _fzf" \
    dl="https://raw.githubusercontent.com/junegunn/fzf/master/shell/key-bindings.zsh -> key-binding.zsh" \
    dl="https://raw.githubusercontent.com/junegunn/fzf/master/man/man1/fzf-tmux.1 -> ${ZPFX}/man/man1/fzf-tmux.1" \
    dl="https://raw.githubusercontent.com/junegunn/fzf/master/man/man1/fzf.1 -> ${ZPFX}/man/man1/fzf.1" \
    src="key-binding.zsh" \
    @junegunn/fzf \
  sbin="**/bat" \
    mv="**/autocomplete/bat.zsh -> _bat" \
    cp="**/bat.1 -> ${ZPFX}/man/man1/bat.1" \
    @sharkdp/bat \
  sbin="**/fd" \
    cp="**/fd.1  -> ${ZPFX}/man/man1/fd.1" \
    @sharkdp/fd \
  sbin="**/hexyl" \
    cp="**/hexyl.1  -> ${ZPFX}/man/man1/hexyl.1" \
    @sharkdp/hexyl \
  sbin="**/ghq" \
    @x-motemen/ghq
